#! perl

use strict;
# use lib qw(EekBoekLibrary);

=head1 NAME

EekBoek - Bookkeeping software for small and medium-size businesses

=head1 SYNOPSIS

  ebgui

EekBoek is a bookkeeping package for small and medium-size businesses.
Unlike other accounting software, EekBoek has both a command-line
interface (CLI) and a graphical user-interface (GUI). Furthermore, it
has a complete Perl API to create your own custom applications.

EekBoek is designed for the Dutch/European market and currently
available in Dutch only. An English translation is in the works (help
appreciated).

=head1 DESCRIPTION

For a description how to use the program, see L<http://www.eekboek.nl/docs/index.html>.

=head1 BUGS AND PROBLEMS

Please use the eekboek-users mailing list at SourceForge.

=head1 AUTHOR AND CREDITS

Johan Vromans (jvromans@squirrel.nl) wrote this package.

Web site: L<http://www.eekboek.nl>.

=head1 COPYRIGHT AND DISCLAIMER

This program is Copyright 2005-2009 by Squirrel Consultancy. All
rights reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of either: a) the GNU General Public License as
published by the Free Software Foundation; either version 1, or (at
your option) any later version, or b) the "Artistic License" which
comes with Perl.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See either the
GNU General Public License or the Artistic License for more details.

=cut

package main;

use strict;
use warnings;

use Wx qw( :everything  );;
# If we get here, we have Wx :)

no warnings qw(redefine once);
local *Wx::App::OnInit = sub{1};

#   setlocale(LC_ALL, "nl_NL");

my $local = Wx::Locale->new("Dutch");
$local->AddCatalog("wxstd");

use File::Spec;
use File::Basename;

# Common case when run from unpacked dist.
my $script = $0;
if ( -s File::Spec->catfile( dirname( dirname ($script) ), "lib", "EekBoek.pm" ) ) {
    unshift( @INC, File::Spec->catfile( dirname( dirname ($script) ), "lib" ) );
}

# Check EekBoek install.
error( "EekBoek is niet geïnstalleerd, of kon niet worden gevonden.\n".
       "\nRaadplaag uw systeembeheerder.\n",
       "Installatiefout" )
  unless findfile( "EekBoek.pm" )
  &&     findfile( "EB.pm" )
  &&     findfile( "EB/schema/eekboek.sql" );

require EekBoek;

error( "De geïnstalleerde versie van EekBoek is niet toereikend.\n".
       "Versie $EekBoek::VERSION is geïnstalleerd terwijl versie 1.05.00 of later\n".
       "is vereist.\n".
       "\nRaadplaag uw systeembeheerder.",
       "Installatiefout" )
  unless $EekBoek::VERSION ge "1.05.00";

# Check EekBoek GUI install.
error( "De EekBoek GUI is niet geïnstalleerd, of kon niet worden gevonden.\n".
       "\nRaadplaag uw systeembeheerder.",
       "Installatiefout" )
  unless findfile( "EB/Wx/Main.pm" )
  &&     findfile( "EB/Wx/IniWiz.pm" );

require EB::Wx::IniWiz;
EB::Wx::IniWiz->run;

################ Subroutines ################

sub findfile {
    my ( $file ) = @_;
    foreach ( @INC ) {
	my $f = File::Spec->catfile( $_, $file );
	return $f if -s $f;
    }
    return;
}

################ Messengers ################

use Encode;
sub _msg {
    my ( $msg, $caption, $style ) = @_;
    $style |= wxOK;
    my $d = Wx::MessageDialog->new ( undef,
				     encode_utf8($msg),
				     encode_utf8($caption),
				     $style, wxDefaultPosition );
    $d->ShowModal;
    $d->Destroy;
}

sub info {
    push( @_, wxICON_INFORMATION );
    goto &_msg;
}

sub error {
    push( @_, wxICON_ERROR );
    &_msg;
    exit(1);
}

sub warning {
    push( @_, wxICON_EXCLAMATION );
    goto &_msg;
}

1;

# $Id: ebgui,v 1.6 2009/10/09 15:45:52 jv Exp $
